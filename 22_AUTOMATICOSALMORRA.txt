LD I_CBPETDESCSALMORRA
TIM 1010 #50
'  Grafo de DOSIFICACION DE ACEITES: Mantenimiento del grafo
LD cierto
//  Permitimos más transiciones el siguiente ciclo
RSET AZS_GRAFOTRANSICION
LDNOT AZS_GRAFODOSIFICACION
ANDNOT AZS_GRAFOPAUSADOSIFICACION
ANDNOT AZS_GRAFOESPERAFINPETICON
ANDNOT AZS_GRAFOREPOSO
SET AZS_GRAFOREPOSO
'  Grafo de DOSIFICACION DE ACEITES: Transiciones
LD AZS_GRAFOREPOSO
ANDNOT AZS_GRAFOTRANSICION
AND=S(302) ZS_ESTADO_INSTALACION &2
AND I_CBPETDESCSALMORRA
OUT TR0
AND=(300) AZS_RecetaConSalmuera &1
OUT TR1
SET AZS_GRAFODOSIFICACION
RSET AZS_GRAFOREPOSO
SET AZS_GRAFOTRANSICION
MOVL(498) &0 AZS_ValorContadorSalmuera
//  Reseteamos el contador
MOV(021) &0 V8_cbEdigitosvisorbajo
MOVL(498) AZS_ConsignaSalmuera V8_cbEconsigna
//  Grabamos la consigna
MOVL(498) &0 AZS_Cantidaddosificada
MOV(021) &1 AZS_EstadoSalmuera
AND<>SL(308) V8_cbEconsigna &0
MOV(021) &1 V8_cbEordendosificar
LD TR1
MOV(021) &1 AZS_EstadoSalmuera
LD TR0
ANDNOT AZS_GRAFOTRANSICION
AND TIM_RET_I_CB_PETDESCARSALMUERA
LD I_CBPETDESCSOLIDOSLIQUIDOS
ANDNOT CA_GRAFOSC2ESPERADESCARGA
ORNOT I_CBPETDESCSOLIDOSLIQUIDOS
ANDLD
AND=(300) AZS_RecetaConSalmuera &0
SET AZS_GRAFOESPERAFINPETICON
//  Si no  hay aceite en la formula  pasamos directamente a confirmar
RSET AZS_GRAFOREPOSO
SET AZS_GRAFOTRANSICION
LD AZS_GRAFODOSIFICACION
ANDNOT AZS_GRAFOTRANSICION
OUT TR0
AND I_CBPETDESCSALMORRA
AND AZS_FinDosificacion
SET AZS_GRAFOESPERAFINPETICON
RSET AZS_GRAFODOSIFICACION
SET AZS_GRAFOTRANSICION
MOV(021) &2 AZS_EstadoSalmuera
LD TR0
ANDNOT AZS_GRAFOTRANSICION
ANDNOT I_CBPETDESCSALMORRA
ANDNOT O_CBFINDESCSALMORRA
SET AZS_GRAFOPAUSADOSIFICACION
RSET AZS_GRAFODOSIFICACION
SET AZS_GRAFOTRANSICION
MOV(021) &0 V8_cbEordendosificar
LD AZS_GRAFOPAUSADOSIFICACION
ANDNOT AZS_GRAFOTRANSICION
AND I_CBPETDESCSALMORRA
SET AZS_GRAFODOSIFICACION
RSET AZS_GRAFOPAUSADOSIFICACION
SET AZS_GRAFOTRANSICION
AND<>SL(308) V8_cbEconsigna &0
AND<>S(307) AZS_EstadoSalmuera &2
MOV(021) &1 V8_cbEordendosificar
LD AZS_GRAFOESPERAFINPETICON
ANDNOT AZS_GRAFOTRANSICION
ANDNOT I_CBPETDESCSALMORRA
AND O_CBFINDESCSALMORRA
SET AZS_GRAFOREPOSO
RSET AZS_GRAFOESPERAFINPETICON
SET AZS_GRAFOTRANSICION
MOV(021) &0 AZS_RecetaConSalmuera
LD VIR_I_PGRESETSALMUERA
RSET AZS_GRAFODOSIFICACION
RSET AZS_GRAFOPAUSADOSIFICACION
RSET AZS_GRAFOESPERAFINPETICON
SET AZS_GRAFOREPOSO
MOV(021) &0 AZS_RecetaConSalmuera
'  Grafo de DOSIFICACION DE ACEITES: Estados
LD AZS_GRAFOREPOSO
//  En estado de reposo no puede haver ninguna orden activa
OUT TR0
MOV(021) &0 V8_cbEordendosificar
AND=S(302) V8_cbSciclodosificacion &1
MOV(021) &1 V8_cbEordenabortar
LD TR0
AND=S(302) V8_cbSciclodosificacion &0
MOV(021) &0 V8_cbEordenabortar
LD TR0
MOV(021) &0 AZS_EstadoSalmuera
RSET AZS_HaEntradoCicloV8
LD AZS_GRAFODOSIFICACION
OUT TR0
AND=S(302) &1 V8_cbSfinaldosificacion
MOV(021) &0 V8_cbEordendosificar
MOVL(498) V8_cbSpesodosificado AZS_Cantidaddosificada
LD TR0
AND<>S(307) V8_cbSfinaldosificacion &1
AND<>SL(308) V8_cbEconsigna &0
MOVL(498) V8_cbSpesoneto AZS_Cantidaddosificada
LD TR0
AND=S(302) &0 V8_cbSciclodosificacion
TIM 2500 #10
LD TR0
AND=S(302) &1 V8_cbSciclodosificacion
SET AZS_HaEntradoCicloV8
LD TR0
AND AZS_HaEntradoCicloV8
AND=S(302) &0 V8_cbSciclodosificacion
MOV(021) &0 V8_cbEordendosificar
MOV(021) &2 AZS_EstadoSalmuera
LD AZS_GRAFODOSIFICACION
AND TIM_AZS_FINDOSIFICACIONSALMORRA
OR 210.00
OUT AZS_FinDosificacion
LD AZS_GRAFOPAUSADOSIFICACION
AND<>S(307) V8_cbSfinaldosificacion &1
AND<>SL(308) V8_cbEconsigna &0
MOVL(498) V8_cbSpesoneto AZS_Cantidaddosificada
LD AZS_GRAFOESPERAFINPETICON
//  Mientras esperamos nos retire la señal de petición actuamos la senyal de fin de dosificación
OUT VIR_O_CBFINDESCSALMORRA_1
'  EJECUCIÓN INCONDICIONAL: Cargamos valores de parametros fijos para llamada de CONTROL BASCULA
LD cierto
MOV(021) &2 V8_cbEdatavalidsigno
'  Si no estamos en Automático y nos piden aceite confirmamos fin de dosificación.
LD=S(302) ZS_ESTADO_INSTALACION &1
AND I_CBPETDESCSALMORRA
OUT VIR_O_CBFINDESCSALMORRA_2
'  Controles manuales del aceite
LD VIR_I_PGZEROSALMUERA
AND=S(302) ZS_ESTADO_INSTALACION &1
MOVL(498) &0 AZS_ValorContadorSalmuera
MOV(021) &0 V8_cbEdigitosvisorbajo
LD VIR_I_PGCONFIRMARERRORSALMUERA
MOV(021) &1 V8_cbEconfirmarerror
LDNOT VIR_I_PGCONFIRMARERRORSALMUERA
MOV(021) &0 V8_cbEconfirmarerror
'  Contaje de impulsos de líquidos
@LD I_CVSALMORRA
+L(401) AZS_ValorContadorSalmuera AZS_ValorPulsoSalmuera AZS_ValorContadorSalmuera
/L(431) AZS_ValorContadorSalmuera &100 AZS_ValorContadorSalmuera_uint
BCD(024) AZS_ValorContadorSalmuera_uint V8_cbEdigitosvisorbajo
'  Alarmas de la sincronización del aceite
LD=S(302) AZS_RecetaConSalmuera &1
TIM 2501 TEM_AZS_ALARMASALMUERA
LD TIM_AZS_ALARMAFALTAPETICIO
OUT A_AZS_FALTAPETICION
'  Union de la activación de la señal de final de aceite.
LD VIR_O_CBFINDESCSALMORRA_1
OR VIR_O_CBFINDESCSALMORRA_2
OUT O_CBFINDESCSALMORRA
'  Envio de las colas y errores máximos
LD cierto
MOVL(498) COLA_SAMORRA V8_cbEcola
MOVL(498) ERROR_SALMORRA V8_cbEerrormaximo
