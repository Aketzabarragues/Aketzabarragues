'  ESTADO DE REPOSO DE LA INSTALACIÓN ZONA PULMON INTEGRAL
LD=(300) ZPI_ESTADO_INSTALACION &0
SET GMZPI_REPOSO
RSET GMZPI_DIRECCIONAMIENTO
RSET GMZPI_SOLO_TRANSPORTE
RSET GMZPI_TRANSPORTE_ALIMENTADO
RSET GMZPI_BARRIDO
'  DISCRIMINACIÓN AUTOMÁTICO/MANUAL ZONA PULMON INTEGRAL
LD MZPI_PET_CargaS9aPulmon_AUT
//  Petición carga Silo 9 a Pulmon AUTOMATICO
AND=(300) ZPI_ESTADO_INSTALACION &2
LD VIR_I_PGCARGAS9aPULMON
//  Petición carga Silo9 a Pulmon MANUAL (pulsador virtual)
AND=(300) ZPI_ESTADO_INSTALACION &1
ORLD
OUT MZPI_PET_CargaS9aPulmon_CAM
LD MZPI_PET_CargaS10aPulmon_AUT
//  Petición carga Silo 10 a Pulmon AUTOMATICO
AND=(300) ZPI_ESTADO_INSTALACION &2
LD VIR_I_PGCARGAS10aPULMON
//  Petición carga Silo10 a Pulmon MANUAL (pulsador virtual)
AND=(300) ZPI_ESTADO_INSTALACION &1
ORLD
OUT MZPI_PET_CargaS10aPulmon_CAM
'  DIRECCIONAMIENTOS DE SILO 9 I 10 A PULMON INTEGRAL (MZPI)
LD MZPI_PET_DireccionamientoS9aPulmon_1
//  Activación de filtro Pulmón Integral en direccionamiento de Silo 9 i 10 a Pulmon Integral
OR MZPI_PET_DireccionamientoS9aPulmon_2
OR MZPI_PET_DireccionamientoS9aPulmon_3
OR MZPI_PET_DireccionamientoS9aPulmon_4
OR MZPI_PET_DireccionamientoS10aPulmon_1
OR MZPI_PET_DireccionamientoS10aPulmon_2
OR MZPI_PET_DireccionamientoS10aPulmon_3
OR MZPI_PET_DireccionamientoS10aPulmon_4
OUT O_LLFILTRO
LD O_LLFILTRO
OUT MZPI_FUN_DireccionamientoS9_10aPulmon
'  TRANSPORTES SILO 9 I 10 A PULMON INTEGRAL (MZPI)
LD MZPI_PET_TransporteS9aPulmon_1
//  Transporte Silo 9, 10 a pulmón Integral (activar M15)
OR MZPI_PET_TransporteS9aPulmon_2
OR MZPI_PET_TransporteS9aPulmon_3
OR MZPI_PET_TransporteS10aPulmon_1
OR MZPI_PET_TransporteS10aPulmon_2
OR MZPI_PET_TransporteS10aPulmon_3
OUT MZPI_PET_O_M15
LD MZPI_FUN_M15
AND MZPI_FUN_DireccionamientoS9_10aPulmon
OUT MZPI_FUN_TransporteS9_10aPulmon
'  ARRANQUE DEL MOTOR M15
LD MZPI_PET_O_M15
OR MZPI_FUN_M15_var
AND I_DCM15
OUT TR0
OUT O_KLM15
AND I_RCM15
OUT MZPI_FUN_M15
LD TR0
LD TIM_MZPI_CAMBIO_ET_M15
OR O_KTM15
ANDLD
ANDNOT O_KEM15
OUT O_KTM15
LD TR0
ANDNOT MZPI_FUN_M15
OUT TR0
ANDNOT TIM_MZPI_ARRANQUE_M15
ANDNOT O_KTM15
OUT O_KEM15
LD TR0
TIM 1000 #70
AND TIM_MZPI_ARRANQUE_M15
TIM 1001 #3
@LD MZPI_FUN_M15
//  Paro retardado Motor M15
SET MZPI_FUN_M15_var
LD MZPI_FUN_M15_var
//  Tiempo de paro de motor M15
ANDNOT MZPI_PET_O_M15
TIM 1002 TEM_MZPI_PARO_M15
LD TIM_MZPI_PARO_M15
RSET MZPI_FUN_M15_var
'  GRAFO DE CONTROL DEL TRANSPORTE: MANTENIMIENTO DEL GRAFO
LD cierto
RSET GMZPI_TRANSICION
LDNOT GMZPI_REPOSO
ANDNOT GMZPI_DIRECCIONAMIENTO
ANDNOT GMZPI_SOLO_TRANSPORTE
ANDNOT GMZPI_TRANSPORTE_ALIMENTADO
ANDNOT GMZPI_BARRIDO
SET GMZPI_REPOSO
'  GRAFO DE CONTROL DEL TRANSPORTE: TRANSICIONES
LD GMZPI_REPOSO
ANDNOT GMZPI_TRANSICION
LD MZPI_PET_CargaS9aPulmon_CAM
OR MZPI_PET_CargaS10aPulmon_CAM
ANDLD
OUT TR0
SET GMZPI_DIRECCIONAMIENTO
RSET GMZPI_REPOSO
SET GMZPI_TRANSICION
AND MZPI_PET_CargaS9aPulmon_CAM
MOV(021) &9 MZPI_SILOTRANSPORTE
LD TR0
AND MZPI_PET_CargaS10aPulmon_CAM
MOV(021) &10 MZPI_SILOTRANSPORTE
LD GMZPI_DIRECCIONAMIENTO
ANDNOT GMZPI_TRANSICION
OUT TR0
AND MZPI_FUN_DireccionamientoS9_10aPulmon
SET GMZPI_SOLO_TRANSPORTE
RSET GMZPI_DIRECCIONAMIENTO
SET GMZPI_TRANSICION
LD TR0
ANDNOT GMZPI_TRANSICION
LD=(300) MZPI_SILOTRANSPORTE &9
ANDNOT MZPI_PET_CargaS9aPulmon_CAM
LD=(300) MZPI_SILOTRANSPORTE &10
ANDNOT MZPI_PET_CargaS10aPulmon_CAM
ORLD
ANDLD
SET GMZPI_REPOSO
RSET GMZPI_DIRECCIONAMIENTO
SET GMZPI_TRANSICION
LD GMZPI_SOLO_TRANSPORTE
ANDNOT GMZPI_TRANSICION
OUT TR0
AND MZPI_FUN_TransporteS9_10aPulmon
ANDNOT I_GAVACUOESTATOM15
SET GMZPI_TRANSPORTE_ALIMENTADO
RSET GMZPI_SOLO_TRANSPORTE
SET GMZPI_TRANSICION
LD TR0
ANDNOT GMZPI_TRANSICION
LD=(300) MZPI_SILOTRANSPORTE &9
ANDNOT MZPI_PET_CargaS9aPulmon_CAM
LD=(300) MZPI_SILOTRANSPORTE &10
ANDNOT MZPI_PET_CargaS10aPulmon_CAM
ORLD
ANDLD
SET GMZPI_REPOSO
RSET GMZPI_SOLO_TRANSPORTE
SET GMZPI_TRANSICION
LD TR0
ANDNOT GMZPI_TRANSICION
ANDNOT MZPI_FUN_DireccionamientoS9_10aPulmon
SET GMZPI_DIRECCIONAMIENTO
RSET GMZPI_SOLO_TRANSPORTE
SET GMZPI_TRANSICION
LD GMZPI_TRANSPORTE_ALIMENTADO
ANDNOT GMZPI_TRANSICION
OUT TR0
LD=(300) MZPI_SILOTRANSPORTE &9
ANDNOT MZPI_PET_CargaS9aPulmon_CAM
LD=(300) MZPI_SILOTRANSPORTE &10
ANDNOT MZPI_PET_CargaS10aPulmon_CAM
ORLD
ANDLD
SET GMZPI_BARRIDO
RSET GMZPI_TRANSPORTE_ALIMENTADO
SET GMZPI_TRANSICION
LD TR0
ANDNOT GMZPI_TRANSICION
ANDNOT MZPI_FUN_TransporteS9_10aPulmon
SET GMZPI_SOLO_TRANSPORTE
RSET GMZPI_TRANSPORTE_ALIMENTADO
SET GMZPI_TRANSICION
LD TR0
ANDNOT GMZPI_TRANSICION
AND I_GAVACUOESTATOM15
SET GMZPI_SOLO_TRANSPORTE
RSET GMZPI_TRANSPORTE_ALIMENTADO
SET GMZPI_TRANSICION
LD GMZPI_BARRIDO
ANDNOT GMZPI_TRANSICION
AND TIM_MZPI_BARRIDO
SET GMZPI_REPOSO
RSET GMZPI_BARRIDO
SET GMZPI_TRANSICION
'  GRAFO DE CONTROL DEL TRANSPORTE: ESTADOS
LD GMZPI_DIRECCIONAMIENTO
//  Direccionamiento
OUT TR0
AND=(300) MZPI_SILOTRANSPORTE &9
OUT MZPI_PET_DireccionamientoS9aPulmon_1
LD TR0
AND=(300) MZPI_SILOTRANSPORTE &10
OUT MZPI_PET_DireccionamientoS10aPulmon_1
LD GMZPI_SOLO_TRANSPORTE
//  Solo Transporte
OUT TR0
AND=(300) MZPI_SILOTRANSPORTE &9
OUT MZPI_PET_DireccionamientoS9aPulmon_2
OUT MZPI_PET_TransporteS9aPulmon_1
LD TR0
AND=(300) MZPI_SILOTRANSPORTE &10
OUT MZPI_PET_DireccionamientoS10aPulmon_2
OUT MZPI_PET_TransporteS10aPulmon_1
LD GMZPI_TRANSPORTE_ALIMENTADO
//  Transporte Alimentado
OUT TR0
AND=(300) MZPI_SILOTRANSPORTE &9
OUT MZPI_PET_DireccionamientoS9aPulmon_3
OUT MZPI_PET_TransporteS9aPulmon_2
OUT MZPI_PET_FluidificacionS9
OUT O_KKENS9
OUT O_EAVMS9
AND I_RCENS9
ANDNOT I_FCVMS9
OUT O_KKSD9
OUT MZPI_FUN_AlimentacionS9aPulmon
LD TR0
AND=(300) MZPI_SILOTRANSPORTE &10
OUT MZPI_PET_DireccionamientoS10aPulmon_3
OUT MZPI_PET_TransporteS10aPulmon_2
OUT MZPI_PET_FluidificacionS10
OUT O_KKENS10
OUT O_EAVMS10
AND I_RCENS10
ANDNOT I_FCVMS10
AND P_Off
OUT O_KKSD10
OUT MZPI_FUN_AlimentacionS10aPulmon
LD GMZPI_BARRIDO
//  BARRIDO, Tiempo de barrido de transporte Silo9 i 10 a Pulmón Integral
OUT TR0
AND=(300) MZPI_SILOTRANSPORTE &9
OUT MZPI_PET_DireccionamientoS9aPulmon_4
OUT MZPI_PET_TransporteS9aPulmon_3
LD TR0
AND=(300) MZPI_SILOTRANSPORTE &10
OUT MZPI_PET_DireccionamientoS10aPulmon_4
OUT MZPI_PET_TransporteS10aPulmon_3
LD TR0
TIM 1003 TEM_MZPI_BARRIDO
'  FLUIDIFICACIÓN SILO 9
LD MZPI_PET_FluidificacionS9
//  Fluidificación de solo el Silo 9 MZPI
OR MHSC2_PET_FluidificacionS9
//  Fluidificación de solo el Silo 9 MHSC2
ANDNOT MZPI_FluidificacionS10ACTIVA
OUT MZPI_FluidificacionS9ACTIVA
OUT MZPI_PET_O_VF9_10_1
OUT O_KKDIS9
AND MZPI_FUN_VF9_10
AND I_RCDIS9
OUT MZPI_PET_O_ECVMFS10_1
LD MZPI_FUN_VF9_10
AND I_RCDIS9
AND O_ECVMFS10
OUT MZPI_FUN_FluidificaciónS9
'  FLUIDIFICACIÓN SILO 10
LD MZPI_PET_FluidificacionS10
//  Fluidificación de solo el Silo 10 MZPI
OR MHSC2_PET_FluidificacionS10
//  Fluidificación de solo el Silo 10 MHSC2
ANDNOT MZPI_FluidificacionS9ACTIVA
OUT MZPI_FluidificacionS10ACTIVA
OUT MZPI_PET_O_VF9_10_2
OUT O_KKDIS10
AND MZPI_FUN_VF9_10
AND I_RCDIS10
OUT MZPI_PET_O_ECVMFS9_1
LD MZPI_FUN_VF9_10
AND I_RCDIS10
AND O_ECVMFS9
OUT MZPI_FUN_FluidificaciónS10
'  MARCHA/PARO MOTOR VF9/10 VENTILADOR FLUIDIFICACIÓN
LD MZPI_PET_O_VF9_10_1
OR MZPI_PET_O_VF9_10_2
OR MZPI_FUN_VF9_10_var
OUT TR0
AND T1011
OUT O_KKVF9_10
LD TR0
AND I_RCVF9_10
TIM 1004 TEM_MZPI_ARRANQUE_VF9_10
LD TR0
AND TIM_MZPI_ARRANQUE_VF9_10
OUT MZPI_FUN_VF9_10
LD TR0
ANDNOT MZPI_FUN_VF9_10
OUT MZPI_PET_O_ECVMFS9_2
OUT MZPI_PET_O_ECVMFS10_2
'  retardo para que la turbina fluidificadora del silo 9 y 10 arranque cuando tenga menos inercia
LD MZPI_PET_O_VF9_10_1
OR MZPI_PET_O_VF9_10_2
OR MZPI_FUN_VF9_10_var
OR T1012
TIM 1011 #300
'  tiempo para anular los 30 segundos del tim 1011 para cuando este mas parada de dicho tiempo el tim 1012 nos dara permiso para arrancar la turbina para su arranque
LDNOT O_KKVF9_10
TIM 1012 #50
@LD MZPI_FUN_VF9_10
//  Enclavamiento motor
SET MZPI_FUN_VF9_10_var
LD MZPI_FUN_VF9_10_var
//  Tiempo de paro de motor ventilador fluidificación VF9/10
ANDNOT MZPI_PET_O_VF9_10_1
ANDNOT MZPI_PET_O_VF9_10_2
TIM 1005 TEM_MZPI_PARO_VF9_10
LD TIM_MZPI_PARO_VF9_10
//  Paro motor
RSET MZPI_FUN_VF9_10_var
'  CONTROLES ACTIVACIÓN DE MÚLTIPLES EQUIPOS
LD MZPI_PET_O_ECVMFS9_1
//  Cerrar válvula VMFS9
OR MZPI_PET_O_ECVMFS9_2
OUT O_ECVMFS9
LD MZPI_PET_O_ECVMFS10_1
//  Cerrar válvula VMFS10
OR MZPI_PET_O_ECVMFS10_2
OUT O_ECVMFS10
'  SECCIÓN ALARMAS ZONA PULMON INTEGRAL
LD O_EAVMS9
//  Alarmas VMS9 no abre
AND I_FCVMS9
TIM 1007 #50
LD TIM_A_MZPI_O_EAVMS9_C
OUT A_MZPI_O_EAVMS9_C
LDNOT O_EAVMS9
//  VMS9 no cierra
ANDNOT I_FCVMS9
TIM 1006 #50
LD TIM_A_MZPI_N_O_EAVMS9_A
OUT A_MZPI_N_O_EAVMS9_A
LD O_EAVMS10
//  VMS10 no abre
AND I_FCVMS10
AND P_Off
TIM 1008 #50
LD TIM_A_MZPI_O_EAVMS10_C
OUT A_MZPI_O_EAVMS10_C
LDNOT O_EAVMS10
//  VMS10 no cierra
ANDNOT I_FCVMS10
AND P_Off
TIM 1009 #50
LD TIM_A_MZPI_N_O_EAVMS10_A
OUT A_MZPI_N_O_EAVMS10_A
LD O_KKENS9
//  Alarma contactor motor ENS9 no cierra
ANDNOT I_RCENS9
OUT A_MZPI_O_KKENS9_A
LDNOT O_KKENS9
//  Alarma contactor motor ENS9 cerrado
AND I_RCENS9
OUT A_MZPI_N_O_KKENS9_C
LD O_KKENS10
//  Alarma contactor motor ENS10 no cierra
ANDNOT I_RCENS10
OUT A_MZPI_O_KKENS10_A
LDNOT O_KKENS10
//  Alarma contactor ENS10 cerrado
AND I_RCENS10
OUT A_MZPI_N_O_KKENS10_C
LD O_KKDIS9
//  Alarma contactor motor DIS9 no cierra
ANDNOT I_RCDIS9
OUT A_MZPI_O_KKDIS9_A
LDNOT O_KKDIS9
//  Alarma contactor motor DIS9 cerrado
AND I_RCDIS9
OUT A_MZPI_N_O_KKDIS9_C
LD O_KKDIS10
//  Alarma contactor motor DIS10 no cierra
ANDNOT I_RCDIS10
OUT A_MZPI_O_KKDIS10_A
LDNOT O_KKDIS10
//  Alarma contactor motor DIS10 cerrado
AND I_RCDIS10
OUT A_MZPI_N_O_KKDIS10_C
LD O_KKVF9_10
//  Alarma contactor motor fluidificación VF9/10 no cierra
ANDNOT I_RCVF9_10
OUT A_MZPI_O_KKVF9_A
LDNOT O_KKVF9_10
//  Alarma contactor de motor fluidificación VF9/10 cerrado
AND I_RCVF9_10
OUT A_MZPI_N_O_KKVF9_C
LD O_KKSD9
//  Alarma contactor motor SD9 no cierra
ANDNOT I_RCSD9
OUT A_MZPI_O_KKSD9_A
LDNOT O_KKSD9
//  Alarma contactor motor SD9 cerrado
AND I_RCSD9
OUT A_MZPI_N_O_KKSD9_C
LD O_KKSD10
//  Alarma contactor motor SD10 no cierra
ANDNOT I_RCSD10
OUT A_MZPI_O_KKSD10_A
LDNOT O_KKSD10
//  Alarma contator motor SD10 cerrado
AND I_RCSD10
OUT A_MZPI_N_O_KKSD10_C
LD O_KLM15
//  Alarma contactor M15 no cierra
AND O_KTM15
ANDNOT I_RCM15
OUT A_MZPI_O_KLM15_A
LDNOT O_KLM15
//  Alarma contactor M15 cerrado
ANDNOT O_KTM15
AND I_RCM15
OUT A_MZPI_N_O_KLM15_C
LDNOT I_DCM15
//  Alarma disyuntor motor M15 abierto
OUT A_MZPI_N_I_DCM15
LD I_GAVACUOESTATOM15
//  Alarma sobrepresión en soplante pulmón integral
OUT A_MZPI_I_GAVACUOESTATOM15
