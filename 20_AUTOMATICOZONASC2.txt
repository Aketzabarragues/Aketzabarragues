'  GESTION DE COLA DE PETICIONES
LD=(300) ASC2_EstadoColaPeticiones &0
//  La cola de peticiones esta en reposo
AND=S(302) ZSC2_ESTADO_INSTALACION &2
AND I_SSHABILITAR_COLA
AND<>S(307) PSC2_BaseColaPeticiones &0
//  Si la cola no esta vacia
AND<>S(307) PSC2_BaseCantidadPeticiones &0
LD=S(302) ASC2_UltimaRecetaEjecutada PSC2_BaseColaPeticiones
//  Si la receta és la misma que la última ejecutada o és diferente y hay confirmación
LD<>S(307) ASC2_UltimaRecetaEjecutada PSC2_BaseColaPeticiones
AND VIR_I_PGCONFSEGUIRCOLA
ORLD
ANDLD
MOV(021) &1 ASC2_EstadoColaPeticiones
//  Pasamos al estado de petición de producto
MOV(021) PSC2_BaseColaPeticiones ASC2_UltimaRecetaEjecutada
//  Gravamos la última receta ejecutada
MOV(021) PSC2_BaseCantidadPeticiones D16210
//  Almacenamos los valores en el historico, antes de hacerles Reset
++(590) D7099
MOV(021) D7099 D16200
//  
MOV(021) PSC2_BaseColaPeticiones D16220
OUT SIGUIENTE_MASA
MOV(021) PSC2_BaseColaPeticiones D16008
//  Guardamos numero de receta antes de borrarla (GB Connected)
AND>=(325) D7099 PSC2_BaseCantidadPeticiones
MOV(021) &0 PSC2_BaseColaPeticiones
//  Borramos la receta ejecutada
OUT SIGUIENTE_RECETA
MOV(021) &0 D7099
LD=(300) ASC2_EstadoColaPeticiones &1
@AND CA_GRAFOSC2REPOSO
MOV(021) &0 ASC2_EstadoColaPeticiones
//  Pasamos al estado de lectura de cola pues ya ha finalizado la receta
LD VIR_I_PGRESETHARLIQING
MOV(021) &0 ASC2_EstadoColaPeticiones
//  Pasamos al estado de lectura de cola si pasan el punto a manual
LD=(300) ASC2_EstadoColaPeticiones &1
//  Estamos en ejecución de receta. Pedimos la ejecución al módulo de comunicación con la amasadora.
MOV(021) &1 ASC2_PeticionReceta
MOV(021) ASC2_UltimaRecetaEjecutada P1_crEnumeroreceta
LD=(300) ASC2_EstadoColaPeticiones &0
//  Estamos en reposo. Paramos la petición.
MOV(021) &0 ASC2_PeticionReceta
LD cierto
//  Borramos los zeros de los espacios intermedios de las colas.
MOV(021) &0 ASC2_Contador
MOV(021) &1 ASC2_Contador2
FOR(512) &7
LD cierto
OUT TR0
COLL(081) PSC2_BaseColaPeticiones ASC2_Contador ASC2_Auxiliar
AND=S(302) ASC2_Auxiliar &0
COLL(081) PSC2_BaseColaPeticiones ASC2_Contador2 ASC2_Auxiliar2
DIST(080) ASC2_Auxiliar2 PSC2_BaseColaPeticiones ASC2_Contador
DIST(080) &0 PSC2_BaseColaPeticiones ASC2_Contador2
COLL(081) PSC2_BaseCantidadPeticiones ASC2_Contador2 ASC2_Auxiliar2
DIST(080) ASC2_Auxiliar2 PSC2_BaseCantidadPeticiones ASC2_Contador
DIST(080) &0 PSC2_BaseCantidadPeticiones ASC2_Contador2
LD TR0
++(590) ASC2_Contador
++(590) ASC2_Contador2
NEXT(513)
'  Mantenimiento del grafo SC2 ( SOLIDOS+LIQUIDOS (excepto azúcar y sal))
LD cierto
RSET CA_GRAFOSC2TRANSICION
LDNOT CA_GRAFOSC2REPOSO
//  Si no hay ningún estado activo activamos el de reposo
ANDNOT CA_GRAFOSC2INTERMEDIOREPOPREPA
ANDNOT CA_GRAFOSC2PREPARACION
ANDNOT CA_GRAFOSC2ESPERAAZUCAR
ANDNOT CA_GRAFOSC2ESPERAACEITE
ANDNOT CA_GRAFOSC2ESPERADESCARGA
ANDNOT CA_GRAFOSC2DESCARGA
ANDNOT CA_GRAFOSC2PAUSADESCARGA
ANDNOT CA_GRAFOSC2ESPERAFINAZUCAR
ANDNOT CA_GRAFOSC2ESPERAFINCOMUNICA
ANDNOT CA_GRAFOSC2ESPERAFININGREDIADOR
SET CA_GRAFOSC2REPOSO
'  Transiciones del grafo SC2 ( SOLIDOS+LIQUIDOS (excepto azúcar y sal))
LD CA_GRAFOSC2REPOSO
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) ZSC2_ESTADO_INSTALACION &2
AND=S(302) ASC2_PeticionReceta &1
SET CA_GRAFOSC2INTERMEDIOREPOPREPA
RSET CA_GRAFOSC2REPOSO
SET CA_GRAFOSC2TRANSICION
MOVL(498) &0 MISC2_ValorContadorAgua
//  Ponemos a zero el contador del agua
MOV(021) &0 V5_cbEdigitosvisorbajo
MOV(021) &1 P1_crEordenpreparacionreceta
LD CA_GRAFOSC2INTERMEDIOREPOPREPA
ANDNOT CA_GRAFOSC2TRANSICION
SET CA_GRAFOSC2ESPERAFININGREDIADOR
RSET CA_GRAFOSC2INTERMEDIOREPOPREPA
SET CA_GRAFOSC2TRANSICION
MOV(021) P1_crScodigoAzucar ASC2_CodigoAzucarProxima
//  Copiamos el azúcar y sal de la receta
MOVL(498) P1_crScantidadAzucar ASC2_CantidadAzucarTeoricaProxima
MOV(021) P1_crScodigoSal ASC2_CodigoSalProxima
//  Copiamos el azúcar y sal de la receta
MOVL(498) P1_crScantidadSal ASC2_CantidadSalTeoricaProxima
LD<>(305) P1_crScodigoAzucar &0
OR<>(305) P1_crScodigoSal &0
ANDLD
MOV(021) P1_crEnumeroreceta ASC2_NRecetaProximaAzuSal
LD CA_GRAFOSC2ESPERAFININGREDIADOR
ANDNOT CA_GRAFOSC2TRANSICION
ANDNOT CA_SC2INTERVIENEINGREDIADOR
SET CA_GRAFOSC2PREPARACION
RSET CA_GRAFOSC2ESPERAFININGREDIADOR
SET CA_GRAFOSC2TRANSICION
LD CA_GRAFOSC2PREPARACION
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) ZSC2_ESTADO_INSTALACION &2
AND=S(302) P1_crSrecetacargada &1
SET CA_GRAFOSC2ESPERAAZUCAR
RSET CA_GRAFOSC2PREPARACION
SET CA_GRAFOSC2TRANSICION
MOV(021) &0 P1_crEordenpreparacionreceta
LD CA_GRAFOSC2ESPERAAZUCAR
ANDNOT CA_GRAFOSC2TRANSICION
LD CA_GRAFOAZUSALESPDESCARGAAZUCAR
//  Comprovamos que el azúcar tambien este preparado para descargar
LD=(300) ASC2_CodigoAzucarProxima &0
AND CA_GRAFOAZUSALREPOSO
//  Comprovamos que el azúcar tambien este preparado para descargar
ORLD
ANDLD
SET CA_GRAFOSC2ESPERAACEITE
RSET CA_GRAFOSC2ESPERAAZUCAR
SET CA_GRAFOSC2TRANSICION
LD CA_GRAFOSC2ESPERAACEITE
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) AZA_RecetaConAceite &0
AND=S(302) AZS_RecetaConSalmuera &0
SET CA_GRAFOSC2ESPERADESCARGA
RSET CA_GRAFOSC2ESPERAACEITE
SET CA_GRAFOSC2TRANSICION
LD CA_GRAFOSC2ESPERADESCARGA
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) ZSC2_ESTADO_INSTALACION &2
ANDNOT CA_GRAFOAZUSALESPDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALPAUSADESCAZUCAR
AND I_CBPETDESCSOLIDOSLIQUIDOS
OUT TR0
SET CA_GRAFOSC2DESCARGA
RSET CA_GRAFOSC2ESPERADESCARGA
SET CA_GRAFOSC2TRANSICION
AND<>(305) P1_crScodigoAceite &0
OUT TR1
MOVL(498) P1_crScantidadAceite AZA_ConsignaAceite
MOV(021) P1_crScodigoAceite AZA_CodigoAceiteDosificar
MOVL(498) &0 AZA_Cantidaddosificada
AND=S(302) ZA_ESTADO_INSTALACION &2
MOVL(498) &0 AZA_ValorContadorAceite
MOV(021) &0 V4_cbEdigitosvisorbajo
LD TR1
MOV(021) &1 AZA_RecetaConAceite
LD TR0
MOV(021) P1_crEnumeroreceta AZA_RecetaCurso
AND<>(305) P1_crScodigoSalmuera &0
OUT TR1
MOVL(498) P1_crScantidadSalmuera AZS_ConsignaSalmuera
MOVL(498) &0 AZS_Cantidaddosificada
AND=S(302) ZS_ESTADO_INSTALACION &2
MOVL(498) &0 AZS_ValorContadorSalmuera
MOV(021) &0 V8_cbEdigitosvisorbajo
LD TR1
MOV(021) &1 AZS_RecetaConSalmuera
LD TR0
MOV(021) P1_crEnumeroreceta AZS_RecetaCurso
MOV(021) &1 P1_crEordendescargavisor1
MOV(021) &1 P1_crEordendescargavisor3
LD CA_GRAFOSC2DESCARGA
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) ZSC2_ESTADO_INSTALACION &2
OUT TR0
AND=S(302) P1_crScicloreceta &1
ANDNOT I_CBPETDESCSOLIDOSLIQUIDOS
SET CA_GRAFOSC2PAUSADESCARGA
RSET CA_GRAFOSC2DESCARGA
SET CA_GRAFOSC2TRANSICION
MOV(021) &0 P1_crEordendescargavisor1
MOV(021) &0 P1_crEordendescargavisor3
LD TR0
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) P1_crScicloreceta &0
SET CA_GRAFOSC2ESPERAFINAZUCAR
RSET CA_GRAFOSC2DESCARGA
SET CA_GRAFOSC2TRANSICION
MOV(021) &0 P1_crEordendescargavisor1
MOV(021) &0 P1_crEordendescargavisor3
LD CA_GRAFOSC2PAUSADESCARGA
ANDNOT CA_GRAFOSC2TRANSICION
AND=S(302) ZSC2_ESTADO_INSTALACION &2
AND I_CBPETDESCSOLIDOSLIQUIDOS
SET CA_GRAFOSC2DESCARGA
RSET CA_GRAFOSC2PAUSADESCARGA
SET CA_GRAFOSC2TRANSICION
MOV(021) &1 P1_crEordendescargavisor1
MOV(021) &1 P1_crEordendescargavisor3
LD CA_GRAFOSC2ESPERAFINAZUCAR
ANDNOT CA_GRAFOSC2TRANSICION
ANDNOT CA_GRAFOAZUSALPREPARACIONAZUCAR
ANDNOT CA_GRAFOAZUSALDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALESPDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALPAUSADESCAZUCAR
SET CA_GRAFOSC2ESPERAFINCOMUNICA
RSET CA_GRAFOSC2ESPERAFINAZUCAR
SET CA_GRAFOSC2TRANSICION
LD CA_GRAFOSC2ESPERAFINCOMUNICA
ANDNOT CA_GRAFOSC2TRANSICION
ANDNOT I_CBPETDESCSOLIDOSLIQUIDOS
AND O_CBFINDESCSOLIDOSLIQUIDOS
SET CA_GRAFOSC2REPOSO
RSET CA_GRAFOSC2ESPERAFINCOMUNICA
SET CA_GRAFOSC2TRANSICION
'  Si abortamos vamos directamente al estado inicial
LD VIR_I_PGRESETHARLIQING
RSET CA_GRAFOSC2DESCARGA
RSET CA_GRAFOSC2ESPERADESCARGA
RSET CA_GRAFOSC2PAUSADESCARGA
RSET CA_GRAFOSC2INTERMEDIOREPOPREPA
RSET CA_GRAFOSC2PREPARACION
RSET CA_GRAFOSC2ESPERAAZUCAR
RSET CA_GRAFOSC2ESPERAACEITE
RSET CA_GRAFOSC2ESPERAFINAZUCAR
RSET CA_GRAFOSC2ESPERAFININGREDIADOR
RSET CA_GRAFOSC2ESPERAFINCOMUNICA
SET CA_GRAFOSC2REPOSO
MOV(021) &0 P1_crEordenpreparacionreceta
//  Al realizar el reset desactivamos todos los procesos
MOV(021) &0 P1_crEordendescargavisor1
MOV(021) &0 P1_crEordendescargavisor3
MOV(021) &0 ASC2_CodigoAzucarProxima
RSET CA_SC2INTERVIENEINGREDIADOR
'  Estados de los grafos (SOLIDOS+LIQUIDOS (excepto azúcar y sal))
LD CA_GRAFOSC2REPOSO
OUT TR0
AND=S(302) P1_crScicloreceta &1
MOV(021) &1 P1_crEordenabortarreceta
LD TR0
AND=S(302) P1_crScicloreceta &0
MOV(021) &0 P1_crEordenabortarreceta
LD TR0
MOV(021) &0 P1_crEordendescargavisor1
MOV(021) &0 P1_crEordendescargavisor3
MOV(021) &0 P1_crEordenpreparacionreceta
//  Al realizar el reset desactivamos todos los procesos
LD CA_GRAFOSC2PREPARACION
LD MISC2_PET_Carga_AGUACAL_CAM
OR MISC2_PET_Carga_AGUAFRIA_CAM
ANDLD
SET CA_SC2INTERVIENEINGREDIADOR
LD CA_GRAFOSC2DESCARGA
OR CA_GRAFOSC2PAUSADESCARGA
UP(521)
OUT ZSC2_FLANCOINICIODESCARGA
LD CA_GRAFOSC2DESCARGA
OR CA_GRAFOSC2PAUSADESCARGA
ANDNOT ZSC2_FLANCOINICIODESCARGA
AND=(300) P2_crScicloreceta &0
OUT ZSC2_INDFINALDESCARGASAL_1
LD CA_GRAFOSC2DESCARGA
OR CA_GRAFOSC2ESPERAFINAZUCAR
OR CA_GRAFOSC2ESPERAFINCOMUNICA
OR CA_GRAFOSC2REPOSO
OR CA_GRAFOSC2INTERMEDIOREPOPREPA
OR CA_GRAFOSC2ESPERAFININGREDIADOR
AND CA_SC2INTERVIENEINGREDIADOR
OUT TR0
OUT MISC2_PET_BombaIngrediador_AUTO
AND I_NMVACIOINGREDIADOR
TIM 2102 TEM_AZCS2_VACIADOINGREDIADOR
LD TR0
LD TIM_AZCS2_VACIADOINGREDIADOR
OR PulsIngredVacio
ANDLD
RSET CA_SC2INTERVIENEINGREDIADOR
LD CA_GRAFOSC2PREPARACION
OR CA_GRAFOSC2ESPERAAZUCAR
OR CA_GRAFOSC2ESPERAACEITE
OR CA_GRAFOSC2ESPERADESCARGA
AND CA_SC2INTERVIENEINGREDIADOR
AND I_PGDESINHIBIRAGITADOR
OUT MISC2_PET_AgitadorIngrediador_AUTO
LD CA_GRAFOSC2ESPERAFINCOMUNICA
TIM 2100 #2
AND TIM_AZCS2_FINCICLOCOMUNICA
OUT O_CBFINDESCSOLIDOSLIQUIDOS
'  GRAFO AZUCAR-SAL - Mantenimiento del grafo
LD cierto
RSET CA_GRAFOAZUSALTRANSICION
LDNOT CA_GRAFOAZUSALREPOSO
ANDNOT CA_GRAFOAZUSALPREPARACIONAZUCAR
ANDNOT CA_GRAFOAZUSALESPDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALPAUSADESCAZUCAR
ANDNOT CA_GRAFOAZUSALPREPARACIONSAL
ANDNOT CA_GRAFOAZUSALESPDESCARGASAL
ANDNOT CA_GRAFOAZUSALDESCARGASAL
ANDNOT CA_GRAFOAZUSALPAUSADESCSAL
ANDNOT CA_GRAFOAZUSALESPERAFINCOM
SET CA_GRAFOAZUSALREPOSO
'  GRAFO AZUCAR-SAL - Transiciones del grafo
LD CA_GRAFOAZUSALREPOSO
ANDNOT CA_GRAFOAZUSALTRANSICION
AND<>(305) ASC2_NRecetaProximaAzuSal &0
SET CA_GRAFOAZUSALPREPARACIONAZUCAR
RSET CA_GRAFOAZUSALREPOSO
SET CA_GRAFOAZUSALTRANSICION
BSET(071) &0 D4000 D4036
//  Borramos receta 12 productos por receta
MOV(021) ASC2_CodigoAzucarProxima ASC2_CodigoAzucarCurso
//  Copiamos la consigna de la cantidad de AZUCAR a dosificar
MOV(021) &0 ASC2_CodigoAzucarProxima
MOVL(498) ASC2_CantidadAzucarTeoricaProxima ASC2_CantidadAzucarTeoricaCurso
MOVL(498) &0 ASC2_CantidadAzucarTeoricaProxima
MOV(021) ASC2_CodigoSalProxima ASC2_CodigoSalCurso
//  Copiamos la consigna de la cantidad de SAL a dosificar
MOV(021) &0 ASC2_CodigoSalProxima
MOVL(498) ASC2_CantidadSalTeoricaProxima ASC2_CantidadSalTeoricaCurso
MOVL(498) &0 ASC2_CantidadSalTeoricaProxima
MOV(021) ASC2_NRecetaProximaAzuSal ASC2_NumRecetaCursoAzuSal
//  Grabamos la receta que estamos dosificando
MOV(021) &0 ASC2_NRecetaProximaAzuSal
MOV(021) ASC2_CodigoAzucarCurso P2_Receta1CodigoAzucar
//  Grabamos la receta que vamos a dosificar con AZUCAR
MOV(021) ASC2_CodigoAzucarCurso D17003
MOVL(498) ASC2_CantidadAzucarTeoricaCurso P2_Receta1CantidadAzucar
MOVL(498) ASC2_CantidadAzucarTeoricaCurso D17001
MOV(021) ASC2_CodigoSalCurso P2_Receta1CodigoSal
//  Grabamos la receta que vamos a dosificar con AZUCAR
MOVL(498) ASC2_CantidadSalTeoricaCurso P2_Receta1CantidadSal
MOV(021) &1 P2_crEnumeroreceta
//  Para AZUCAR i SAL siempre usamos la receta uno
MOV(021) &1 P2_crEordenpreparacionreceta
//  Para AZUCAR i SAL siempre usamos la receta uno
LD CA_GRAFOAZUSALPREPARACIONAZUCAR
ANDNOT CA_GRAFOAZUSALTRANSICION
AND=(300) P2_crSrecetacargada &1
SET CA_GRAFOAZUSALESPDESCARGAAZUCAR
RSET CA_GRAFOAZUSALPREPARACIONAZUCAR
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordenpreparacionreceta
//  Para AZUCAR i SAL siempre usamos la receta uno
LD CA_GRAFOAZUSALESPDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALTRANSICION
LD CA_GRAFOSC2ESPERADESCARGA
OR CA_GRAFOSC2REPOSO
OR CA_GRAFOSC2DESCARGA
OR CA_GRAFOSC2PAUSADESCARGA
ANDLD
AND I_CBPETDESCSOLIDOSLIQUIDOS
SET CA_GRAFOAZUSALDESCARGAAZUCAR
RSET CA_GRAFOAZUSALESPDESCARGAAZUCAR
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &1 P2_crEordendescargavisor2
//  Activamos la descarga del azúcar
LD CA_GRAFOAZUSALDESCARGAAZUCAR
ANDNOT CA_GRAFOAZUSALTRANSICION
OUT TR0
AND=(300) P2_crScicloreceta &0
SET CA_GRAFOAZUSALREPOSO
//  No hay sal a dosificar, pasamos a estado de reposo
RSET CA_GRAFOAZUSALDESCARGAAZUCAR
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordendescargavisor2
//  Desactivamos la descarga del azúcar
LD TR0
ANDNOT CA_GRAFOAZUSALTRANSICION
ANDNOT I_CBPETDESCSOLIDOSLIQUIDOS
SET CA_GRAFOAZUSALPAUSADESCAZUCAR
RSET CA_GRAFOAZUSALDESCARGAAZUCAR
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordendescargavisor2
//  Desactivamos la descarga del azúcar
LD CA_GRAFOAZUSALPAUSADESCAZUCAR
ANDNOT CA_GRAFOAZUSALTRANSICION
AND I_CBPETDESCSOLIDOSLIQUIDOS
SET CA_GRAFOAZUSALDESCARGAAZUCAR
RSET CA_GRAFOAZUSALPAUSADESCAZUCAR
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &1 P2_crEordendescargavisor2
//  Activamos la descarga del azúcar
LD CA_GRAFOAZUSALPREPARACIONSAL
ANDNOT CA_GRAFOAZUSALTRANSICION
AND=(300) P2_crSrecetacargada &1
SET CA_GRAFOAZUSALESPDESCARGASAL
RSET CA_GRAFOAZUSALPREPARACIONSAL
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordenpreparacionreceta
//  Para AZUCAR i SAL siempre usamos la receta uno
LD CA_GRAFOAZUSALESPDESCARGASAL
ANDNOT CA_GRAFOAZUSALTRANSICION
AND I_CBPETDESCSAL
SET CA_GRAFOAZUSALDESCARGASAL
RSET CA_GRAFOAZUSALESPDESCARGASAL
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &1 P2_crEordendescargavisor2
//  Activamos la descarga del azúcar
LD CA_GRAFOAZUSALDESCARGASAL
ANDNOT CA_GRAFOAZUSALTRANSICION
OUT TR0
AND=(300) P2_crScicloreceta &0
SET CA_GRAFOAZUSALESPERAFINCOM
//  No hay sal a dosificar, pasamos a estado de reposo
RSET CA_GRAFOAZUSALDESCARGASAL
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordendescargavisor2
//  Desactivamos la descarga del azúcar
LD TR0
ANDNOT CA_GRAFOAZUSALTRANSICION
ANDNOT I_CBPETDESCSAL
SET CA_GRAFOAZUSALPAUSADESCSAL
RSET CA_GRAFOAZUSALDESCARGASAL
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &0 P2_crEordendescargavisor2
//  Desactivamos la descarga del azúcar
LD CA_GRAFOAZUSALPAUSADESCSAL
ANDNOT CA_GRAFOAZUSALTRANSICION
AND I_CBPETDESCSAL
SET CA_GRAFOAZUSALDESCARGASAL
RSET CA_GRAFOAZUSALPAUSADESCSAL
SET CA_GRAFOAZUSALTRANSICION
MOV(021) &1 P2_crEordendescargavisor2
//  Activamos la descarga del azúcar
LD CA_GRAFOAZUSALESPERAFINCOM
ANDNOT CA_GRAFOAZUSALTRANSICION
ANDNOT I_CBPETDESCSAL
AND O_CBFINDESCSAL
SET CA_GRAFOAZUSALREPOSO
//  Hemos acabado la comunicación con el PLC
RSET CA_GRAFOAZUSALESPERAFINCOM
SET CA_GRAFOAZUSALTRANSICION
'  Si abortamos vamos directamente al estado inicial
LD CA_GRAFOAZUSALPREPARACIONAZUCAR
OR CA_GRAFOAZUSALESPDESCARGAAZUCAR
OR CA_GRAFOAZUSALDESCARGAAZUCAR
OR CA_GRAFOAZUSALPAUSADESCAZUCAR
OR CA_GRAFOAZUSALREPOSO
AND VIR_I_PGRESETHARLIQING
RSET CA_GRAFOAZUSALPREPARACIONAZUCAR
RSET CA_GRAFOAZUSALESPDESCARGAAZUCAR
RSET CA_GRAFOAZUSALDESCARGAAZUCAR
RSET CA_GRAFOAZUSALPAUSADESCAZUCAR
RSET CA_GRAFOAZUSALPREPARACIONSAL
RSET CA_GRAFOAZUSALESPDESCARGASAL
RSET CA_GRAFOAZUSALDESCARGASAL
RSET CA_GRAFOAZUSALPAUSADESCSAL
RSET CA_GRAFOAZUSALESPERAFINCOM
SET CA_GRAFOAZUSALREPOSO
MOV(021) &0 ASC2_CodigoSalProxima
'  Si abortamos vamos directamente al estado inicial
LD CA_GRAFOAZUSALPREPARACIONSAL
OR CA_GRAFOAZUSALESPDESCARGASAL
OR CA_GRAFOAZUSALDESCARGASAL
OR CA_GRAFOAZUSALPAUSADESCSAL
OR CA_GRAFOAZUSALESPERAFINCOM
AND VIR_I_PGRESETSAL
RSET CA_GRAFOAZUSALPREPARACIONAZUCAR
RSET CA_GRAFOAZUSALESPDESCARGAAZUCAR
RSET CA_GRAFOAZUSALDESCARGAAZUCAR
RSET CA_GRAFOAZUSALPAUSADESCAZUCAR
RSET CA_GRAFOAZUSALPREPARACIONSAL
RSET CA_GRAFOAZUSALESPDESCARGASAL
RSET CA_GRAFOAZUSALDESCARGASAL
RSET CA_GRAFOAZUSALPAUSADESCSAL
RSET CA_GRAFOAZUSALESPERAFINCOM
SET CA_GRAFOAZUSALREPOSO
'  GRAFO AZUCAR-SAL - Estados del grafo
LD CA_GRAFOAZUSALREPOSO
OUT TR0
MOV(021) &0 ASC2_CodigoAzucarCurso
//  Inicializamos los valores
MOVL(498) &0 ASC2_CantidadAzucarTeoricaCurso
MOV(021) &0 ASC2_CodigoSalCurso
MOVL(498) &0 ASC2_CantidadSalTeoricaCurso
MOV(021) &0 ASC2_NumRecetaCursoAzuSal
MOV(021) &0 P2_crEordendescargavisor2
MOV(021) &0 P2_crEordenpreparacionreceta
AND=(300) P2_crScicloreceta &1
MOV(021) &1 P2_crEordenabortarreceta
//  Si la receta esta en ciclo la abortamos
LD TR0
AND=(300) P2_crScicloreceta &0
MOV(021) &0 P2_crEordenabortarreceta
LD CA_GRAFOAZUSALESPERAFINCOM
OUT ZSC2_INDFINALDESCARGASAL_2
LD CA_GRAFOAZUSALPREPARACIONSAL
OR CA_GRAFOAZUSALESPDESCARGASAL
TIM 2101 TEM_ZSC2_ALARMASAL
LD ZSC2_INDFINALDESCARGASAL_1
OR ZSC2_INDFINALDESCARGASAL_2
OUT O_CBFINDESCSAL
LD TIM_AZCS2_ALARMAFALTASAL
OUT A_ZSC2_ALARMAPEDIRFALTASAL
LD I_CBPETDESCSAL
AND CA_GRAFOAZUSALPREPARACIONSAL
OUT A_AZSC2_LASALNOESTAPREPARADA
//  Alarma la sal no esta preparada aun
LD I_CBPETDESCSAL
ANDNOT CA_GRAFOAZUSALPREPARACIONSAL
ANDNOT CA_GRAFOAZUSALESPDESCARGASAL
ANDNOT CA_GRAFOAZUSALDESCARGASAL
ANDNOT CA_GRAFOAZUSALPAUSADESCSAL
ANDNOT CA_GRAFOAZUSALESPERAFINCOM
OUT A_ZSC2_NOHAYSALFORMULA
//  No hay sal en esta formula
'  Calculo de proporciones de agua fria, agua caliente en la tolva de liquidos de SC2
LD cierto
AND=(300) P1_crSproductovisor3 &6
*L(421) V3_cbEconsigna ASC2_PorcentajeFriaAgua ASC2_AuxiliarCalculoPorcentajeLong
MOVL(498) ASC2_AuxiliarCalculoPorcentajeLong ASC2_AuxiliarCalculoPorcentajeDoble
/L(431) ASC2_AuxiliarCalculoPorcentajeDoble &100 ASC2_AuxiliarCalculoPorcentajeDoble
MOVL(498) ASC2_AuxiliarCalculoPorcentajeDoble P1_Lento6Agua
'  Calculo de proporciones de agua fria, agua caliente en el ingrediador
LD cierto
AND=(300) P1_crSproductovisor5 &7
*L(421) V5_cbEconsigna ASC2_PorcentajeFriaIngrediador ASC2_AuxiliarCalculoPorcentajeLong
MOVL(498) ASC2_AuxiliarCalculoPorcentajeLong ASC2_AuxiliarCalculoPorcentajeDoble
/L(431) ASC2_AuxiliarCalculoPorcentajeDoble &100 ASC2_AuxiliarCalculoPorcentajeDoble
MOVL(498) ASC2_AuxiliarCalculoPorcentajeDoble P1_Lento7AguaIngrediador
'  Asignación de las colas,errores maximos y lentos correspondientes al silo seleccionado en ASIGNACIÓN PRODUCTO SILO A DOSIFICAR
LD cierto
OUT TR0
AND=S(302) ZSC2_SILO_CARGA_INTEGRAL &3
MOV(021) P1_ColaSilo3 P1_Cola3Integral
MOV(021) P1_LentoSilo3 P1_Lento3Integral
MOV(021) P1_ErrorSilo3 P1_Error3Integral
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &6
MOV(021) P1_ColaSilo6 P1_Cola2Panificable
MOV(021) P1_LentoSilo6 P1_Lento2Panificable
MOV(021) P1_ErrorSilo6 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &1
MOV(021) P1_ColaSilo1 P1_Cola2Panificable
MOV(021) P1_LentoSilo1 P1_Lento2Panificable
MOV(021) P1_ErrorSilo1 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &2
MOV(021) P1_ColaSilo2 P1_Cola2Panificable
MOV(021) P1_LentoSilo2 P1_Lento2Panificable
MOV(021) P1_ErrorSilo2 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &4
MOV(021) P1_ColaSilo4 P1_Cola2Panificable
MOV(021) P1_LentoSilo4 P1_Lento2Panificable
MOV(021) P1_ErrorSilo4 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &5
MOV(021) P1_ColaSilo5 P1_Cola2Panificable
MOV(021) P1_LentoSilo5 P1_Lento2Panificable
MOV(021) P1_ErrorSilo5 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &9
MOV(021) P1_ColaSilo9 P1_Cola2Panificable
MOV(021) P1_LentoSilo9 P1_Lento2Panificable
MOV(021) P1_ErrorSilo9 P1_Error2Panificable
LD TR0
AND=S(302) ZSC2_SILO_CARGA_PANIFICABLE &10
MOV(021) P1_ColaSilo10 P1_Cola2Panificable
MOV(021) P1_LentoSilo10 P1_Lento2Panificable
MOV(021) P1_ErrorSilo10 P1_Error2Panificable
'  ALARMAS
LD=(300) V1_cbSfaltacarga &1
AND T1181
OUT A_ZSC2_FALTAMATCARGAV1
LD=(300) V1_cbSfaltadescarga &1
OUT A_ZSC2_FALTAMATDESCARGAV1
LD=(300) V2_cbSfaltacarga &1
OUT A_ZSC2_FALTAMATCARGAV2
LD=(300) V2_cbSfaltadescarga &1
OUT A_ZSC2_FALTAMATDESCARGAV2
LD=(300) V3_cbSfaltacarga &1
OUT A_ZSC2_FALTAMATCARGAV3
LD=(300) V3_cbSfaltadescarga &1
OUT A_ZSC2_FALTAMATDESCARGAV3
LD=(300) V4_cbSfaltacarga &1
OUT A_ZSC2_FALTAMATCARGAV4
LD CA_GRAFOSC2ESPERAFININGREDIADOR
SET ZSC2_NOPERMITIRALAFALTMATINGR
LDNOT CA_GRAFOSC2ESPERAFININGREDIADOR
TIM 2104 #100
LD TIM_AZSC2_HABILITARALARMAINGREDI
RSET ZSC2_NOPERMITIRALAFALTMATINGR
LD=(300) V5_cbSfaltacarga &1
ANDNOT ZSC2_NOPERMITIRALAFALTMATINGR
OUT A_ZSC2_FALTAMATCARGAV5
'  Alarmas de comprovación básculas
LDTST(350) P1_crAUXbitsgraforeceta &8
OUT A_AZSC2_BASCULASNOVACIASSOLLIQ
LDTST(350) P2_crAUXbitsgraforeceta &8
OUT A_AZSC2_BASCULASNOVACIASAZUSAL
