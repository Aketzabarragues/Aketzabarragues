'  PETICION CARGA VISOR 5 (INGREDIOR) EN AUTOMATICO
LD=S(302) V5_cbScargaproducto &1
AND CA_GRAFOSC2PREPARACION
OUT TR0
AND=S(302) V5_cbScargarapida &0
OUT MISC2_PET_Carga_AGUAFRIA_AUT
LD TR0
AND=S(302) V5_cbScargarapida &1
OUT MISC2_PET_Carga_AGUACAL_AUT
'  DISCRIMINACIÓN AUTOMÁTICO/MANUAL MANIOBRA INGREDIADOR ZONA SC2
LD MISC2_PET_Carga_AGUACAL_AUT
//  Petición carga AGUA CALIENTE INGREDIADOR en AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGINGRED_AGUACAL
//  Petición carga AGUA CALIENTE INGREDIADOR en MANUAL
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MISC2_PET_Carga_AGUACAL_CAM
LD MISC2_PET_Carga_AGUAFRIA_AUT
//  Petición carga AGUA FRIA INGREDIADOR en AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGINGRED_AGUAFRIA
//  Petición carga AGUA FRIA INGREDIADOR en MANUAL
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MISC2_PET_Carga_AGUAFRIA_CAM
LD MISC2_PET_BombaIngrediador_AUTO
//  Petición bomba descarga ingrediador en AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGDESCARGAINGREDIA
//  Petición bomba descarga ingrediador en MANUAL
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MISC2_PET_BombaIngrediador_CAM
LD MISC2_PET_AgitadorIngrediador_AUTO
//  Petición agitador  ingrediador en AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGAGITADORINGRED
//  Petición agitador descarga ingrediador en MANUAL
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MISC2_PET_AgitadorIngrediador_CAM
'  MANIOBRA CARGA AGUA CALIENTE, FRIA INGREDIADOR
LD MISC2_PET_Carga_AGUACAL_CAM
//  Carga Agua Caliente Ingrediador
OUT O_EAACALIENTE2INGREDIADOR
LD MISC2_PET_Carga_AGUACAL_CAM
//  Funciona carga agua caliente
AND O_EAACALIENTE2INGREDIADOR
ANDNOT I_FCACALIENTE2INGREDIADOR
OUT MISC2_FUN_CargaAGUACAL
LD MISC2_PET_Carga_AGUAFRIA_CAM
//  Carga agua fria ingrediador
OUT O_EAAFRIA2INGREDIADOR
LD MISC2_PET_Carga_AGUAFRIA_CAM
//  Funciona carga agua fria 
AND O_EAAFRIA2INGREDIADOR
ANDNOT I_FCAFRIA2INGREDIADOR
OUT MISC2_FUN_CargaAGUAFRIA
LD MISC2_PET_BombaIngrediador_CAM
AND I_DCBOMBAING
OUT O_KKBOMBAING
LDNOT I_NMVACIOINGREDIADOR
TIM 2103 #300
LD MISC2_PET_AgitadorIngrediador_CAM
AND I_DCAGITADORING
AND TIM_AZCS2_AGITADORINGREDIADOR
OUT W17.12
'  Contaje de impulsos de agua ingrediador
@LD I_CVCONTADORAGUA
+L(401) MISC2_ValorContadorAgua MISC2_ValorPulsoAgua MISC2_ValorContadorAgua
/L(431) MISC2_ValorContadorAgua &100 MISC2_ValorContadorAgua_uint
BCD(024) MISC2_ValorContadorAgua_uint V5_cbEdigitosvisorbajo
LD VIR_I_PGZEROINGREDIADOR
AND=S(302) ZSC2_ESTADO_INSTALACION &1
MOVL(498) &0 MISC2_ValorContadorAgua
MOV(021) &0 V5_cbEdigitosvisorbajo
'  ALARMAS
LD O_EAACALIENTE2INGREDIADOR
//  Válvula ACALIENTE2 NO ABRE
AND I_FCACALIENTE2INGREDIADOR
TIM 2300 #50
LD TIM_A_MISC2_O_EAACALIENTE2_C
OUT A_MISC2_O_EAACALIENTE2_C
LDNOT O_EAACALIENTE2INGREDIADOR
//  Válvula ACALIENTE2 NO CIERRA
ANDNOT I_FCACALIENTE2INGREDIADOR
TIM 2301 #50
LD TIM_A_MISC2_N_O_EAACALIENTE2_A
OUT A_MISC2_N_O_EAACALIENTE2_A
LD O_EAAFRIA2INGREDIADOR
//  Válvula AFRIA2 NO ABRE
AND I_FCAFRIA2INGREDIADOR
TIM 2302 #50
LD TIM_A_MISC2_O_EAAFRIA2_C
OUT A_MISC2_O_EAAFRIA2_C
LDNOT O_EAAFRIA2INGREDIADOR
//  Válvula AFRIA2 NO CIERRA
ANDNOT I_FCAFRIA2INGREDIADOR
TIM 2303 #50
LD TIM_A_MISC2_N_O_EAAFRIA2_A
OUT A_MISC2_N_O_EAAFRIA2_A
LDNOT I_DCBOMBAING
//  Alarma bomba de descarga ingrediador
OUT A_ZSC2_N_I_DCBOMBAING
LDNOT I_DCAGITADORING
//  Alarma agitador del ingrediador
OUT A_ZSC2_N_I_DCAGITADORING
