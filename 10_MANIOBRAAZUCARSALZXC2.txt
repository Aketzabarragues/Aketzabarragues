'  ESTADO DE REPOSO DE LA INSTALACIÓN ZONA SC2 MANIOBRA AZUCAR SAL
LD=(300) ZSC2_ESTADO_INSTALACION &0
SET GMASSC2_REPOSO
RSET GMASSC2_DIRECCIONAMIENTO
RSET GMASSC2_SOLO_TRANSPORTE
RSET GMASSC2_TRANSPORTE_ALIMENTADO
RSET GMASSC2_BARRIDO
'  CONTROL PRODUCTO A CARGAR A VISOR 2
LD=S(302) V2_cbScargaproducto &1
//  Visor2 peticion de carga de producto AUTOMATICO
OUT TR0
AND=S(302) P2_crSproductovisor2 &8
//  CODIGO PRODUCTO 8 = AZUCAR
OUT MASSC2_PET_CargaS7aBT2_AUT
//  SILO S7 AZUCAR 
LD TR0
AND=S(302) P2_crSproductovisor2 &9
//  CODIGO PRODUCTO 9 = SAL
OUT MASSC2_PET_CargaSALaBT2_AUT
//  SILO SAL
'  DISCRIMINACIÓN AUTOMÁTICO/MANUAL ZONA SC2 MANIOBRA AZUCAR SAL
LD MASSC2_PET_CargaS7aBT2_AUT
//  Petición carga Silo 7 a BT2 AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGCARGAS7aBT2
//  Petición carga Silo 7 a BT2 MANUAL (pulsador virtual)
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MASSC2_PET_CargaS7aBT2_CAM
LD MASSC2_PET_CargaSALaBT2_AUT
//  Petición carga Silo SAL a BT2 AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGCARGASALaBT2
//  Petición carga Silo SAL a BT2 MANUAL (pulsador virtual)
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MASSC2_PET_CargaSALaBT2_CAM
'  VELOCIDAD DE CARGA RAPIDA VISOR 2 (SILOS 7 Y SAL)
LD=S(302) V2_cbScargarapida &1
//  Petición carga rápida Visor2 en AUTOMATICO
AND=(300) ZSC2_ESTADO_INSTALACION &2
LD VIR_I_PGVEL_RAPIDA_V2
//  Pulsador virtual de carga rapida Visor 2 MANUAL
AND=(300) ZSC2_ESTADO_INSTALACION &1
ORLD
OUT MASSC2_PET_CargaRAPIDA_V2_CAM
'  DIRECCIONAMIENTOS MANIOBRA AZUCAR SAL ZSC2
LD MASSC2_PET_DireccionamientoS7aBT2_1
//  Silo 7 direccionado a BT2
OR MASSC2_PET_DireccionamientoS7aBT2_2
OR MASSC2_PET_DireccionamientoS7aBT2_3
OR MASSC2_PET_DireccionamientoS7aBT2_4
OUT TR0
OUT O_EAVE_A
ANDNOT O_KKAGITADORING
OUT O_EAVMAZUCAR
LD TR0
OUT MASSC2_PET_O_EAVMABT2_1
OUT MASSC2_PET_O_LLVFBT2_1
OUT MAASC2_PET_O_ECVDBT2_1
LDNOT I_FCVE_A
AND I_FCVE_SL
ANDNOT I_FCVMABT2
AND I_FCVMBT2
ANDNOT I_FCVMAZUCAR
OUT MASSC2_FUN_DireccionamientoS7aBT2
LD MASSC2_PET_DireccionamientoSALaBT2_1
//  Silo SAL direccionado a BT2
OR MASSC2_PET_DireccionamientoSALaBT2_2
OR MASSC2_PET_DireccionamientoSALaBT2_3
OR MASSC2_PET_DireccionamientoSALaBT2_4
OUT TR0
ANDNOT O_KKAGITADORING
OUT O_EAVE_SL
LD TR0
OUT MASSC2_PET_O_EAVMABT2_2
OUT MASSC2_PET_O_LLVFBT2_2
OUT MAASC2_PET_O_ECVDBT2_2
LD I_FCVE_A
ANDNOT I_FCVE_SL
ANDNOT I_FCVMABT2
AND I_FCVMBT2
OUT MASSC2_FUN_DireccionamientoSALaBT2
'  TRANSPORTE
LD MASSC2_PET_TransporteS7aBT2_1
//  Transporte de silo 7 a BT2
OR MASSC2_PET_TransporteS7aBT2_2
OR MASSC2_PET_TransporteS7aBT2_3
AND MASSC2_FUN_DireccionamientoS7aBT2
OUT MASSC2_PET_O_M14_1
OUT MASSC2_PET_O_CER12_1
LD MASSC2_FUN_DireccionamientoS7aBT2
AND MASSC2_FUN_M14
AND MASSC2_FUN_CER12
OUT MASSC2_FUN_TransporteS7aBT2
LD MASSC2_PET_TransporteSALaBT2_1
//  Transporte de silo SAL a BT2
OR MASSC2_PET_TransporteSALaBT2_2
OR MASSC2_PET_TransporteSALaBT2_3
AND MASSC2_FUN_DireccionamientoSALaBT2
OUT MASSC2_PET_O_M14_2
OUT MASSC2_PET_O_CER12_2
LD MASSC2_FUN_DireccionamientoSALaBT2
AND MASSC2_FUN_M14
AND MASSC2_FUN_CER12
OUT MASSC2_FUN_TransporteSALaBT2
'  ARRANQUE DEL MOTOR M-14
LD MASSC2_PET_O_M14_1
//  Arranque estrella triangulo motor M14
OR MASSC2_PET_O_M14_2
OR MASSC2_FUN_M14_var
AND I_DCM14
OUT TR0
OUT O_KLM14
AND I_RCM14
OUT MASSC2_FUN_M14
LD TR0
LD TIM_MASSC2_CAMBIO_ET_M14
OR O_KTM14
ANDLD
ANDNOT O_KEM14
OUT O_KTM14
LD MASSC2_PET_CargaS7aBT2_CAM
OR O_KKAGITADORING
OR MASSC2_PET_CargaSALaBT2_CAM
ANDLD
OUT O_ECVDM14
LD TR0
ANDNOT MASSC2_FUN_M14
OUT TR0
ANDNOT TIM_MASSC2_ARRANQUE_M14
ANDNOT O_KTM14
OUT O_KEM14
LD TR0
TIM 1201 #70
AND TIM_MASSC2_ARRANQUE_M14
TIM 1200 #3
LD GMASSC2_BARRIDO
OUT O_KKAGITADORING
@LD MASSC2_FUN_M14
//  Paro retardado motor M14
SET MASSC2_FUN_M14_var
LD MASSC2_FUN_M14_var
ANDNOT MASSC2_PET_O_M14_1
ANDNOT MASSC2_PET_O_M14_2
TIM 1202 TEM_MASSC2_PARO_M14
LD TIM_MASSC2_PARO_M14
RSET MASSC2_FUN_M14_var
'  ARRANQUE DEL MOTOR CER-12
LD MASSC2_PET_O_CER12_1
OR MASSC2_PET_O_CER12_2
OR O_KKAGITADORING
AND I_FCTAPACER12
AND O_EAVE_A
OUT O_KKCER12
LD MASSC2_PET_O_CER12_1
OR MASSC2_PET_O_CER12_2
OR O_KKAGITADORING
AND I_FCTAPACER12
OUT MASSC2_FUN_CER12
'  ARRANQUE MOTORES SD7,SDSAL I VARIADOR SVF2.1
LD MASSC2_PET_O_SD7
//  Activación y enclavamiento contactores motores SD7 i SDSAL
OR MASSC2_PET_O_SDSAL
OR TIM_MASSC2_PAROSD7SAL
OUT TR0
RSET O_KKSD7
RSET O_KKSDSAL
AND MASSC2_PET_O_SD7
AND W506.00
SET O_KKSD7
LD TR0
AND MASSC2_PET_O_SDSAL
AND W506.01
SET O_KKSDSAL
LD MASSC2_PET_O_SDSAL
OUT TR0
AND O_KKSDSAL
AND P_1s
OUT 21.09
LD TR0
AND 8.14
AND 8.15
OUT 19.14
OUT 19.15
LD MASSC2_PET_O_SD7
OUT TR0
ANDNOT T0051
TIM 0050 #120
LD TR0
AND T0050
TIM 0051 #70
LD TR0
ANDNOT T0051
LD TR0
AND T0050
KEEP(011) W506.00
LD MASSC2_PET_O_SDSAL
OUT TR0
ANDNOT T0053
TIM 0052 #100
LD TR0
AND T0052
TIM 0053 #60
LD TR0
ANDNOT T0053
AND T0102
ANDNOT 20.07
LD TR0
AND T0052
KEEP(011) W506.01
LD MASSC2_PET_O_SDSAL
TIM 0102 #120
LD I_RCSD7SAL
//  Tiempo de paro de motores sinfin SD7 i SDSAL
ANDNOT MASSC2_PET_O_SD7
ANDNOT MASSC2_PET_O_SDSAL
TIM 1203 TEM_MASSC2_PAROSD7SAL
LD MASSC2_PET_O_SD7
//  Activación variador SVF2.1
OR MASSC2_PET_O_SDSAL
AND I_VOSVF2_1
AND I_RCSD7SAL
AND I_SCSVF2_1
OUT O_VASVF2_1
'  CONTROLES ACTIVACIÓN DE MULTIPLES EQUIPOS
LD MASSC2_PET_O_EAVMABT2_1
//  Activar válvula VMABT2
OR MASSC2_PET_O_EAVMABT2_2
OUT O_EAVMABT2
LD MASSC2_PET_O_LLVFBT2_1
//  Activar válvula VFBT2
OR MASSC2_PET_O_LLVFBT2_2
OR MDASC2_PET_O_LLVFBT2
OUT TR0
LD TIM_MASSC2_MARCHAVFBT2
LD 108.00
AND TIM_MASSC2_MARCHAVFBT2
ORLD
OR MDASC2_PET_O_LLVFBT2
ANDLD
ANDNOT EVdescsolid
OUT SopladMangas
LD TR0
LD MASSC2_PET_O_LLVFBT2_1
//  Activar válvula VFBT2
OR MASSC2_PET_O_LLVFBT2_2
ANDLD
MOV(021) #70 TEM_MASSC2_PAROFILTROBT2
LD TR0
AND MDASC2_PET_O_LLVFBT2
MOV(021) #30 TEM_MASSC2_PAROFILTROBT2
LD TR0
MOV(021) #2 TEM_MASSC2_MARCHAFILTROBT2
ANDNOT TIM_MASSC2_PAROVFBT2
TIM 1206 TEM_MASSC2_PAROFILTROBT2
LD TR0
AND TIM_MASSC2_MARCHAVFBT2
TIM 1207 TEM_MASSC2_MARCHAFILTROBT2
LD O_KKSD7
OR O_KKSDSAL
AND O_VASVF2_1
OUT 108.00
LD MAASC2_PET_O_ECVDBT2_1
//  Cerrar válvula VDBT2
OR MAASC2_PET_O_ECVDBT2_2
OUT O_ECVDBT2
'  GRAFO DE CONTROL DEL TRANSPORTE: MANTENIMIENTO DEL GRAFO
LD cierto
RSET GMASSC2_TRANSICION
LDNOT GMASSC2_REPOSO
ANDNOT GMASSC2_DIRECCIONAMIENTO
ANDNOT GMASSC2_SOLO_TRANSPORTE
ANDNOT GMASSC2_TRANSPORTE_ALIMENTADO
ANDNOT GMASSC2_BARRIDO
SET GMASSC2_REPOSO
'  GRAFO DE CONTROL DEL TRANSPORTE: TRANSICIONES
LD GMASSC2_REPOSO
ANDNOT GMASSC2_TRANSICION
LD MASSC2_PET_CargaS7aBT2_CAM
OR MASSC2_PET_CargaSALaBT2_CAM
ANDLD
OUT TR0
SET GMASSC2_DIRECCIONAMIENTO
RSET GMASSC2_REPOSO
SET GMASSC2_TRANSICION
AND MASSC2_PET_CargaS7aBT2_CAM
MOV(021) &7 MASSC2_SILOTRANSPORTE
LD TR0
AND MASSC2_PET_CargaSALaBT2_CAM
MOV(021) &8 MASSC2_SILOTRANSPORTE
LD GMASSC2_DIRECCIONAMIENTO
ANDNOT GMASSC2_TRANSICION
OUT TR0
LD=(300) MASSC2_SILOTRANSPORTE &7
AND MASSC2_FUN_DireccionamientoS7aBT2
LD=(300) MASSC2_SILOTRANSPORTE &8
AND MASSC2_FUN_DireccionamientoSALaBT2
ORLD
ANDLD
SET GMASSC2_SOLO_TRANSPORTE
RSET GMASSC2_DIRECCIONAMIENTO
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_PET_CargaS7aBT2_CAM
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_PET_CargaSALaBT2_CAM
ORLD
ANDLD
SET GMASSC2_REPOSO
RSET GMASSC2_DIRECCIONAMIENTO
SET GMASSC2_TRANSICION
LD GMASSC2_SOLO_TRANSPORTE
ANDNOT GMASSC2_TRANSICION
OUT TR0
LD=(300) MASSC2_SILOTRANSPORTE &7
AND MASSC2_FUN_TransporteS7aBT2
LD=(300) MASSC2_SILOTRANSPORTE &8
AND MASSC2_FUN_TransporteSALaBT2
ORLD
ANDLD
ANDNOT I_GAVACUOESTATOM14
SET GMASSC2_TRANSPORTE_ALIMENTADO
RSET GMASSC2_SOLO_TRANSPORTE
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_PET_CargaS7aBT2_CAM
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_PET_CargaSALaBT2_CAM
ORLD
ANDLD
SET GMASSC2_REPOSO
RSET GMASSC2_SOLO_TRANSPORTE
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_FUN_DireccionamientoS7aBT2
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_FUN_DireccionamientoSALaBT2
ORLD
ANDLD
SET GMHSC2_DIRECCIONAMIENTO
RSET GMASSC2_SOLO_TRANSPORTE
SET GMASSC2_TRANSICION
LD GMASSC2_TRANSPORTE_ALIMENTADO
ANDNOT GMASSC2_TRANSICION
OUT TR0
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_PET_CargaS7aBT2_CAM
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_PET_CargaSALaBT2_CAM
ORLD
ANDLD
SET GMASSC2_BARRIDO
RSET GMASSC2_TRANSPORTE_ALIMENTADO
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_FUN_DireccionamientoS7aBT2
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_FUN_DireccionamientoSALaBT2
ORLD
ANDLD
SET GMASSC2_SOLO_TRANSPORTE
RSET GMASSC2_TRANSPORTE_ALIMENTADO
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
LD=(300) MASSC2_SILOTRANSPORTE &7
ANDNOT MASSC2_FUN_TransporteS7aBT2
LD=(300) MASSC2_SILOTRANSPORTE &8
ANDNOT MASSC2_FUN_TransporteSALaBT2
ORLD
ANDLD
SET GMASSC2_DIRECCIONAMIENTO
RSET GMASSC2_TRANSPORTE_ALIMENTADO
SET GMASSC2_TRANSICION
LD TR0
ANDNOT GMASSC2_TRANSICION
AND I_GAVACUOESTATOM14
SET GMASSC2_SOLO_TRANSPORTE
RSET GMASSC2_TRANSPORTE_ALIMENTADO
SET GMASSC2_TRANSICION
LD GMASSC2_BARRIDO
ANDNOT GMASSC2_TRANSICION
AND TIM_MASSC2_BARRIDO
SET GMASSC2_REPOSO
RSET GMASSC2_BARRIDO
SET GMASSC2_TRANSICION
'  GRAFO DE CONTROL DEL TRANSPORTE: ESTADOS
LD GMASSC2_DIRECCIONAMIENTO
OUT TR0
AND=(300) MASSC2_SILOTRANSPORTE &7
OUT MASSC2_PET_DireccionamientoS7aBT2_1
LD TR0
AND=(300) MASSC2_SILOTRANSPORTE &8
OUT MASSC2_PET_DireccionamientoSALaBT2_1
LD GMASSC2_SOLO_TRANSPORTE
OUT TR0
AND=(300) MASSC2_SILOTRANSPORTE &7
OUT MASSC2_PET_DireccionamientoS7aBT2_2
OUT MASSC2_PET_TransporteS7aBT2_1
LD TR0
AND=(300) MASSC2_SILOTRANSPORTE &8
OUT MASSC2_PET_DireccionamientoSALaBT2_2
OUT MASSC2_PET_TransporteSALaBT2_1
LD GMASSC2_TRANSPORTE_ALIMENTADO
OUT TR0
AND=(300) MASSC2_SILOTRANSPORTE &7
OUT TR1
OUT MASSC2_PET_DireccionamientoS7aBT2_3
OUT MASSC2_PET_TransporteS7aBT2_2
ANDNOT MASSC2_PET_CargaRAPIDA_V2_CAM
MOV(021) VEL_LENTA_S7 VEL_VARIADOR_SVF2_1
//  Velocidad lenta de carga Silo7 a Visor2
LD TR1
AND MASSC2_PET_CargaRAPIDA_V2_CAM
MOV(021) VEL_RAPIDA_S7 VEL_VARIADOR_SVF2_1
//  Velocidad rápida de carga Silo7 a Visor2
LD TR1
OUT VIR_O_CBFLUIDIFICACIONS7
ANDNOT I_FCVMAZUCAR
TIM 1144 #50
LD TR1
AND TIM_APERTURAVMAZUCAR
OUT MASSC2_PET_O_SD7
LD TR1
AND I_VOSVF2_1
AND I_RCSD7SAL
OUT MASSC2_FUN_AlimentacionS7aBT2
LD TR0
AND=(300) MASSC2_SILOTRANSPORTE &8
OUT TR1
OUT MASSC2_PET_DireccionamientoSALaBT2_3
OUT MASSC2_PET_TransporteSALaBT2_2
ANDNOT MASSC2_PET_CargaRAPIDA_V2_CAM
MOV(021) VEL_LENTA_SAL VEL_VARIADOR_SVF2_1
LD TR1
AND MASSC2_PET_CargaRAPIDA_V2_CAM
MOV(021) VEL_RAPIDA_SAL VEL_VARIADOR_SVF2_1
LD TR1
OUT MASSC2_PET_O_SDSAL
OUT O_KKFVSAL
//  Activar fondo vibrante FVSAL sin nivel minimo de SAL
AND I_NMSAL
TIM 1205 TEM_MASSC2_NivelSAL
//  Tiempo maximo sin nivel minimo de SAL en transporte alimentado
LD TR1
AND I_VOSVF2_1
AND I_RCSD7SAL
OUT MASSC2_FUN_AlimentacionSALaBT2
LD TR0
*(420) VEL_VARIADOR_SVF2_1 &40 VEL_VARIADOR_SVF2_1_AUX
MOV(021) VEL_VARIADOR_SVF2_1_AUX ANA_SFV21SAL2
LD GMASSC2_BARRIDO
OUT TR0
AND=(300) MASSC2_SILOTRANSPORTE &7
OUT MASSC2_PET_DireccionamientoS7aBT2_4
OUT MASSC2_PET_TransporteS7aBT2_3
LD TR0
AND=(300) MASSC2_SILOTRANSPORTE &8
OUT MASSC2_PET_DireccionamientoSALaBT2_4
OUT MASSC2_PET_TransporteSALaBT2_3
LD TR0
TIM 1204 TEM_MASSC2_BARRIDO
//  Tiempo de Barrido MASSC2
'  Cerramos el contactor del polipasto
LD cierto
OUT O_KKPOLIPASTO
'  ALARMAS MANIOBRA AZUCAR I SAL ZSC2
LD EVdescsolid
//  VMBT2 no abre
AND I_FCVMBT2
TIM 1208 #50
LD TIM_A_MASSC2_O_EAVMBT2_C
OUT A_MASSC2_O_EAVMBT2_C
LDNOT EVdescsolid
//  VMBT2 no cierra
ANDNOT I_FCVMBT2
TIM 1209 #50
LD TIM_A_MASSC2_N_O_EAVMBT2_A
OUT A_MASSC2_N_O_EAVMBT2_A
LD O_EAVMABT2
//  VMABT2 no abre
AND I_FCVMABT2
TIM 1210 #50
LD TIM_A_MASSC2_O_EAVMABT2_C
OUT A_MASSC2_O_EAVMABT2_C
LDNOT O_EAVMABT2
//  VMABT2 no cierra
ANDNOT I_FCVMABT2
TIM 1211 #50
LD TIM_A_MASSC2_N_O_EAVMABT2_A
OUT A_MASSC2_N_O_EAVMABT2_A
LD O_EAVE_A
//  VE-A no abre
AND I_FCVE_A
TIM 1212 #50
LD TIM_A_MASSC2_O_EAVE_A_C
OUT A_MASSC2_O_EAVE_A_C
LDNOT O_EAVE_A
//  VE-A no cierra
ANDNOT I_FCVE_A
TIM 1213 #50
LD TIM_A_MASSC2_N_O_EAVE_A_A
OUT A_MASSC2_N_O_EAVE_A_A
LD O_EAVE_SL
//  VE-SL no abre
AND I_FCVE_SL
TIM 1214 #50
LD TIM_A_MASSC2_O_EAVE_SL_C
OUT A_MASSC2_O_EAVE_SL_C
LDNOT O_EAVE_SL
//  VE-SL no cierra
ANDNOT I_FCVE_SL
TIM 1215 #50
LD TIM_A_MASSC2_N_O_EAVE_SL_A
OUT A_MASSC2_N_O_EAVE_SL_A
LD O_KKSD7
//  Contactor motor sinfin SD7 o SDSAL no cierra
OR O_KKSDSAL
ANDNOT I_RCSD7SAL
OUT A_MASSC2_O_KKSD7SAL_A
LDNOT O_KKSD7
//  Contactor motor sinfin SD7 o SDSAL cerrado
ANDNOT O_KKSDSAL
AND I_RCSD7SAL
OUT A_MASSC2_N_O_KKSD7SAL_C
LD O_KKFVSAL
//  Contactor fondo vibrante FVSAL no cierra
ANDNOT I_RCFVSAL
TIM 1148 #50
LD TIM_A_MHSC2_O_KKFVSAL_A
OUT A_MASSC2_O_KKFVSAL_A
LDNOT O_KKFVSAL
//  Contactor motor sinfin FVSAL cerrado
AND I_RCFVSAL
TIM 1147 #50
LD TIM_A_MHSC2_N_O_KKFVSAL_C
OUT A_MASSC2_N_O_KKFVSAL_C
LD O_KKCER12
//  Contactor motor CER12 no cierra
ANDNOT I_RCCER12
TIM 1150 #100
LD TIM_A_MHSCS2_A_O_KKCER12_C
OUT A_MAASC2_O_KKCER12_A
LDNOT O_KKCER12
//  Contactor motor CER12 cerrado
AND I_RCCER12
TIM 1157 #100
LD TIM_A_MHSCS2_A_N_O_KKCER12_A
OUT A_MAASC2_N_O_KKCER12_C
LD O_KLM14
//  Contactor motor M14 no cierra
ANDNOT I_RCM14
AND T0054
OUT A_MAASC2_O_KLM14_A
LD O_KLM14
//  Contactor motor M14 no cierra
TIM 0054 #50
LDNOT O_KLM14
//  Contactor motor M14 cerrado
AND I_RCM14
OUT A_MAASC2_N_O_KLM14_C
LDNOT I_DCM14
//  Disyuntor M14 abierto
OUT A_MASSC2_N_I_DCM14
LDNOT I_VOSVF2_1
//  Variador SVF2.1 no preparado
OUT A_MASSC2_N_I_VOSVF2_1
LDNOT I_SCSVF2_1
//  Relés térmicos motores SD7 o SDAL del variador SVF2.1 abierto
OUT A_MASSC2_N_I_SCSVF2_1
LDNOT I_FCTAPACER12
//  Tapa cernedor CER12 abierta
OUT A_MASSC2_N_I_FCTAPACER12
LD TIM_MASSC2_NivelSAL
//  Tiempo maximo de espera sin nivel minimo de SAL en transporte Alimentado MASSC2
OUT A_MASSC2_N_I_NMSAL
LD I_FCVMMCER12
//  Alarma válvula cernedor CER 12 cerrada
OUT A_MASSC2_I_FCVMMCER12_C
LD I_GAVACUOESTATOM14
//  Alarma  soprepresión motor M14
OUT A_ZSC2_I_GAVACUOESTATOM14
LD O_EAVMAZUCAR
//  Alarma válvula de azúcar no abre VMAZUCAR
AND I_FCVMAZUCAR
OUT A_MASSC2_O_EAVMAZUCAR_C
LDNOT O_EAVMAZUCAR
//  Alarma válvula de azucar no cierra VMAZUCAR
ANDNOT I_FCVMAZUCAR
OUT A_MASSC2_N_O_EAVMAZUCAR_A
